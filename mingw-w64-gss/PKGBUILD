# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gss
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=1.0.3
pkgrel=1
pkgdesc="GNU Generic Security Service (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/gss/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "git" "wget" "texinfo")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-shishi")
source=(https://ftp.gnu.org/gnu/gss/${_realname}-${pkgver}.tar.gz{,.sig}
        001-autoconf.patch
        002-gnulib.patch)
md5sums=('441859b30bed73010d426c087c71aac5'
         'SKIP'
         '1d8b5c6304addb6457f62a7a30b3bdce'
         '65794ac1b601166816f5266f44126ab6')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-autoconf.patch
  patch -p1 -i ${srcdir}/002-gnulib.patch
  touch doc/Makefile.gdoc

  WANT_AUTOMAKE=latest autoreconf -fi

}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make -j1
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
